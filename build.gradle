apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

project.version = '1.0.0'
project.group = 'se.llbit'
project.archivesBaseName = 'collcompare'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
	group = "Reporting"
	reports {
		xml.enabled true
		csv.enabled false
		html.destination "${buildDir}/reports/coverage"
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	// Skip signing when credentials are not available:
	required { project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword') }
	sign configurations.archives
}

uploadArchives {
	repositories.mavenDeployer {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
			if (project.hasProperty('ossrhUsername')) {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}

		pom.project {
			name 'CollectionComparison'
			packaging 'jar'
			description 'A tiny library for comparing collections.'
			url 'https://github.com/llbit/collection-comparison'
			licenses {
				license {
					name 'Modified BSD License'
					url 'http://opensource.org/licenses/BSD-3-Clause'
					distribution 'repo'
				}
			}
			developers {
				developer {
					name 'Jesper Ã–qvist'
					email 'jesper@llbit.se'
				}
			}
			scm {
				connection 'scm:git:https://github.com/llbit/collection-comparison.git'
				url 'https://github.com/llbit/collection-comparison'
			}
		}
	}
}
